/*
 * pdf2htmlEX/build.gradle
 *
 * pdf2htmlEX-Android (https://github.com/ViliusSutkus89/pdf2htmlEX-Android)
 * Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format.
 *
 * Copyright (c) 2019 - 2021 Vilius Sutkus <ViliusSutkus89@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

group = 'com.viliussutkus89'
apply plugin: 'com.android.library'
android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31

        versionCode 25
        versionName project.version

        archivesBaseName = rootProject.name

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk.abiFilters = project.hasProperty('abi') ? [project.property('abi')] : ['arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64']
        externalNativeBuild.cmake.arguments '-DANDROID_STL=c++_shared'
    }
    buildFeatures.prefab true
}
dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.viliussutkus89:assetextractor-android:1.3.2'

    //#UNCOMMENT-DURING-STAGING# implementation 'com.viliussutkus89:pdf2htmlex-android:#STAGING-VERSION#'
    // The line above is used by the ci-scripts/prepareLibraryForStagingRepository
}

//#UNCOMMENT-DURING-STAGING# /* These tasks are not needed for staging lib
apply plugin: 'maven-publish'
apply plugin: 'signing'

def gatherReleaseArtifactsTask = tasks.getByPath(':dependency-builder:gatherReleaseArtifacts')
def gatherDebugArtifactsTask = tasks.getByPath(':dependency-builder:gatherDebugArtifacts')

afterEvaluate {
    tasks.named('mergeReleaseJniLibFolders').get().dependsOn(gatherReleaseArtifactsTask)
    tasks.named('generateReleaseAssets').get().dependsOn(gatherReleaseArtifactsTask)
    tasks.named('mergeDebugJniLibFolders').get().dependsOn(gatherDebugArtifactsTask)
    tasks.named('generateDebugAssets').get().dependsOn(gatherDebugArtifactsTask)
}

android {
    buildTypes {
        release.externalNativeBuild {
            cmake.arguments '-DCMAKE_BUILD_TYPE=MinSizeRel', "-DINSTALLED_PREFIX=${gatherReleaseArtifactsTask.installed.get()}"
        }
        debug.externalNativeBuild {
            cmake.arguments "-DINSTALLED_PREFIX=${gatherDebugArtifactsTask.installed.get()}"
        }
    }
    externalNativeBuild.cmake {
        path 'src/main/cpp/CMakeLists.txt'
        version '3.21.1'
    }
    ndkVersion '22.1.7171670'
    sourceSets {
        release {
            jniLibs.srcDirs += gatherReleaseArtifactsTask.jniLibs.get()
            assets.srcDirs += gatherReleaseArtifactsTask.assets.get()
        }
        debug {
            jniLibs.srcDirs += gatherDebugArtifactsTask.jniLibs.get()
            assets.srcDirs += gatherDebugArtifactsTask.assets.get()
        }
    }
}

configurations {
    javadocDependenciesToObtainClasspath
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.4'
    implementation 'com.viliussutkus89:tmpfile-android:3.0.4'
    implementation 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    implementation 'com.viliussutkus89:ExecutableRunner:1.0.3'

    javadocDependenciesToObtainClasspath 'androidx.annotation:annotation:1.2.0'
    javadocDependenciesToObtainClasspath 'com.getkeepsafe.relinker:relinker:1.4.4'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:tmpfile-android:3.0.4'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:assetextractor-android:1.3.2'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:ExecutableRunner:1.0.3'

    compileOnly project(":dependency-builder")
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("", { from file("../LICENSE")})
    into ("cpp", { from fileTree(dir: "src/main/cpp/") } )
    into ("dependency-builder", { from fileTree(dir: "../dependency-builder/src/main/cpp/") } )
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependenciesToObtainClasspath
    title = "${rootProject.name} ${project.version} API"
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = rootProject.name
                    description = 'Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format'
                    url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                    packaging = 'aar'
                    inceptionYear = '2019'
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                        connection = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android.git'
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }

    tasks.named('generatePomFileForReleasePublication').configure {
        dependsOn(gatherReleaseArtifactsTask)
        doFirst {
            pom.withXml {
                def licensesNode = asNode().appendNode('licenses')

                def thisProjLicense = licensesNode.appendNode('license')
                thisProjLicense.appendNode('name', 'pdf2htmlEX-Android')
                thisProjLicense.appendNode('url', 'https://www.gnu.org/licenses/gpl-3.0.en.html')
                thisProjLicense.appendNode('distribution', 'repo')

                fileTree(dir: gatherReleaseArtifactsTask.licenses.get(), include: '*').visit { depVisitor ->
                    String depName = depVisitor.getName()
                    List<String> licenseText = new ArrayList<String>()

                    fileTree(depVisitor.getFile().getAbsolutePath()).visit { depLicenseFileVisitor ->
                        if (!depLicenseFileVisitor.isDirectory()) {
                            // LGPL2.1 and GPLv2 licenses include form feed (page break) characters.
                            // XML does not take kindly to form feed characters.
                            def licenseTextWithoutFormFeedChar = depLicenseFileVisitor.getFile().getText('UTF-8').replace("\u000C", "\n")
                            licenseText.add(depLicenseFileVisitor.getRelativePath() + "\n\n" + licenseTextWithoutFormFeedChar)
                        }
                    }
                    def depLicenseNode = licensesNode.appendNode('license')
                    depLicenseNode.appendNode('name', depName)
                    depLicenseNode.appendNode('url', licenseText.join("\n\n"))
                    depLicenseNode.appendNode('distribution', 'repo')
                }
            }
        }
    }
}
//#UNCOMMENT-DURING-STAGING# */
