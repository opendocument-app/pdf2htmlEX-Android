plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '0.81.0'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'Poppler is a free software utility library for rendering Portable Document Format (PDF) documents.'
                    url = 'https://poppler.freedesktop.org'
                    packaging = 'aar'
                    inceptionYear = '2005'
                    licenses {
                        license {
                            name = 'GPLv2'
                            url = 'https://www.gnu.org/licenses/gpl-2.0.html'
                            distribution = 'repo'
                        }
                        license {
                            name = 'GPLv3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://cgit.freedesktop.org/poppler/poppler'
                        connection = 'https://anongit.freedesktop.org/git/poppler/poppler.git'
                    }
                    developers {
                        // Developer list obtained from:
                        // https://cgit.freedesktop.org/poppler/poppler/tree/AUTHORS?h=poppler-0.81.0
                        developer {
                            name = 'Derek Noonburg'
                            email = 'derekn@foolabs.com'
                        }
                        developer {
                            name = 'Albert Astals Cid'
                            email = 'aacid@kde.org'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
