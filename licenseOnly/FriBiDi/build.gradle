plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '1.0.12'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'The Free Implementation of the Unicode Bidirectional Algorithm'
                    url = 'https://github.com/fribidi/fribidi'
                    packaging = 'aar'
                    inceptionYear = '2000'
                    licenses {
                        license {
                            name = 'LGPLv2.1-or-later'
                            url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/fribidi/fribidi'
                        connection = 'https://github.com/fribidi/fribidi.git'
                    }
                    developers {
                        // Developer list obtained from:
                        // https://github.com/fribidi/fribidi/blob/v1.0.12/AUTHORS
                        developer {
                            name = 'Behdad Esfahbod'
                            email = 'behdad@gnu.org'
                        }
                        developer {
                            name = 'Dov Grobgeld'
                            email = 'dov.grobgeld@gmail.com'
                        }
                        developer {
                            name = 'Roozbeh Pournader'
                            email = 'roozbeh@gnu.org'
                        }
                        developer {
                            name = 'Khaled Hosny'
                            email = 'khaledhosny@eglug.org'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
